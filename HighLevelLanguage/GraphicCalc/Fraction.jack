// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/09/Fraction/Fraction.jack

/** Represents the Fraction type and related operations. */
class Fraction {
  field int numerator, denominator;  // field = property = member variable.

  /** Constructs a (reduced) fraction from the given numerator and denominator. */
  constructor Fraction new(int x, int y) {
    let numerator = x;
    let denominator = y;
    if (~(y > 0)) {
      let denominator = 1;
    }
    do reduce();   // reduces the fraction
    return this;   // a constructor is expected to return a reference to the new object
  }

  // Reduces this fraction.
  method void reduce() {
    var int g;
    let g = Fraction.gcd(numerator, denominator);
    if (g > 1) {
       let numerator = numerator / g;
       let denominator = denominator / g;
    }
    return;
  }

  /** Accessors. */
  method int getNumerator() { return numerator; }
  method int getDenominator() { return denominator; }

  /** Returns the sum of this fraction and the other one. */
  method Fraction plus(Fraction other) {
    var int sum;
    let sum = (numerator * other.getDenominator()) + (other.getNumerator() * denominator);
    return Fraction.new(sum, denominator * other.getDenominator());
  }

  // More fraction-related methods (minus, times, div, etc.) can be added here.

	/**************************************************************/
	/** The following methods written by Shira Joseph, Dec 2018  **/
	/**----------------------------------------------------------**/

  /** Subtracts one fraction from another */
  method Fraction minus(Fraction sub) {
    var int minuend, subtrahend, difference;
    let minuend = numerator * sub.getDenominator();
    let subtrahend = sub.getNumerator() * denominator;
    let difference = minuend - subtrahend;
    return Fraction.new(difference, denominator * sub.getDenominator());
  }

  /** Multiplies two fractions */
  method Fraction times(Fraction mult) {
    var int numer, denom;
    let numer = numerator * mult.getNumerator();
    let denom = denominator * mult.getDenominator();
    return Fraction.new(numer, denom);
  }

  /** Divides one fraction by another */
  method Fraction dividedBy (Fraction divisor) {
    var int numer, denom, quotient;
    let numer = numerator * divisor.getDenominator();
    let denom = denominator * divisor.getNumerator();
    return Fraction.new(numer, denom);
  }

	/** Turns a string into a Fraction */
  function Fraction toFraction (String num) {
    var int delimiter, forwardSlash, backSlash, n, d, start, end;
    var String numer, denom, num2, num3;
    var char f, b;
    var Fraction frac;
		let f = 47;
		let b = 92;
    let forwardSlash = S.indexOf(num, f);
    		let num2=num;
    let backSlash = S.indexOf(num2, b);
    if (forwardSlash = -1 & backSlash = -1) {

      let n = num.intValue();
      let frac = Fraction.new(n, 1);
      return frac;
    } else {

	    if (forwardSlash > -1) {
	      let delimiter = forwardSlash;
	    } else {
	      let delimiter = backSlash;
	    }

			let num3 = num;
	    let numer = S.substr(num3, 0, delimiter);

			let start = delimiter + 1;
			let end = String.length(num);
	    let denom = S.substr(num, start, end);

			let n = numer.intValue();
			let d = denom.intValue();
			let frac = Fraction.new(n, d);
	    return frac;
    }
  }

method String toString(){
		var String appended, n, d;
		let n = String.new(20);
		let d = String.new(20);
		do n.setInt(numerator);
		do d.setInt(denominator);
		if (denominator = 1){
			if (~(numerator = 1)) {
			let appended = n;
			} else {
				let appended = "";
			}
		} else {
			let appended = S.append3(n, "/", d);
		}
		return appended;
	}

	method void checkValid() {
		if(denominator > 9999 | denominator < -9999 | numerator > 9999 | numerator < -9999){
			do Out.ln("Invalid number");
			do Sys.halt();
		}
		return;
	}

	method boolean isGreaterThan(Fraction f2) {
		if (numerator * f2.getDenominator() > f2.getNumerator() * denominator){
			return true;
		}
		return false;
	}

	method int toInt() {
		var int a;
		let a = numerator/denominator;
		return a;
	}

	/**- - - - - - - - - - - - - - - - - - - - - - - - - - - - - **/
	/**               End of lines by Shira Joseph               **/
	/**************************************************************/

  /** Disposes this fraction. */
  method void dispose() {
    do Memory.deAlloc(this);  // uses an OS routine to recycle the memory held by the object
    return;
  }

  /** Prints this fraction in the format x/y. */
  method void print() {
    do Output.printInt(numerator);
    do Output.printString("/");
    do Output.printInt(denominator);
    return;
  }

  // Computes the greatest common divisor of the given integers.
  function int gcd(int a, int b) {
    var int r;
    while (~(b = 0)) {             // applies Euclid's algorithm
       let r = a - (b * (a / b));  // r = remainder of the integer division a/b
       let a = b; let b = r;
    }
    return a;
  }
}
